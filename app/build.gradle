apply plugin: 'com.android.application'

android {
    def APP_DOMAIN = "APP_DOMAIN"
    def CONFIG = { k -> "\"${project.properties.get(k)}\"" }

    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]
    useLibrary 'org.apache.http.legacy'

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        applicationId "com.vessel.gather"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        flavorDimensions "versionCode"
        testInstrumentationRunner rootProject.ext.dependencies["androidJUnitRunner"]
        vectorDrawables.useSupportLibrary = true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }

        ndk { abiFilters "armeabi" }

        buildConfigField "String", APP_DOMAIN, CONFIG("debug.server.url")
    }

    aaptOptions { cruncherEnabled = false }

    signingConfigs {
        release {
            keyAlias 'juji'
            keyPassword 'juji2018'
            storeFile file('../juji.jks')
            storePassword 'juji2018'
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            buildConfigField "boolean", "USE_CANARY", "true"
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "String", APP_DOMAIN, CONFIG("debug.server.url")
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"
            buildConfigField "boolean", "USE_CANARY", "false"
            minifyEnabled false
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "Juji_${variant.productFlavors[0].name}_v${variant.versionName}_${variant.buildType.name}.apk"
        }
    }

    productFlavors {
        external {  //外部正式
            buildConfigField "String", APP_DOMAIN, CONFIG("release.external.server.url")
        }
        internal {  //内部正式
            buildConfigField "String", APP_DOMAIN, CONFIG("release.internal.server.url")
        }
    }

    lintOptions {
        disable 'InvalidPackage'
        disable "ResourceType"
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["design"]

    // test
    testImplementation rootProject.ext.dependencies["junit"]
    testImplementation rootProject.ext.dependencies["canary-release"]
    debugImplementation rootProject.ext.dependencies["canary-debug"]
    releaseImplementation rootProject.ext.dependencies["canary-release"]

    // tool
    implementation 'me.jessyan:arms:2.4.1'
    annotationProcessor rootProject.ext.dependencies["dagger2-compiler"]
    annotationProcessor(rootProject.ext.dependencies["butterknife-compiler"]) {
        exclude module: 'support-annotations'
    }
    implementation rootProject.ext.dependencies["retrofit-url-manager"]
    implementation "com.github.ywp0919:AddressPickerLib:1.0.1"
    implementation 'com.github.CHAOHUILI:AddressPicker:v1.1.0'
    implementation 'com.github.CymChad:BaseRecyclerViewAdapterHelper:2.9.32'
    implementation rootProject.ext.dependencies["arouter"]
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]
    implementation "com.github.miguelbcr:RxPaparazzo:0.6.0-2.x"
    implementation 'com.tencent.bugly:crashreport_upgrade:1.2.0'

    // layout
    implementation rootProject.ext.dependencies["fragmentation"]
    implementation rootProject.ext.dependencies["fragmentation-core"]
    implementation rootProject.ext.dependencies["FlycoTabLayout_Lib"]
    implementation rootProject.ext.dependencies["FlycoSystemBar_Lib"]
}
